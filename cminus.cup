/*
  Created by: Fei Song, Silas Wright
  File Name: cminus.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
        valid = false;
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

    /* Assume true at the start, but set to false with parsing errors */
    public static boolean valid = true;

    private ExpList singleton(Exp exp) {
        if (exp == null) {
            return null;
        }
        return new ExpList(exp, null);
    }

    private ExpList append(ExpList list, Exp exp) {
        if (exp == null) {
            return list;
        }
        if (list == null) {
            return new ExpList(exp, null);
        }
        ExpList t = list;
        while (t.tail != null) {
            t = t.tail;
        }
        t.tail = new ExpList(exp, null);
        return list;
    }

    private ExpList concat(ExpList first, ExpList second) {
        if (first == null) {
            return second;
        }
        ExpList t = first;
        while (t.tail != null) {
            t = t.tail;
        }
        t.tail = second;
        return first;
    }

    private Exp packListAsCommaExpr(ExpList list, int row, int col) {
        if (list == null) {
            return null;
        }
        Exp result = list.head;
        ExpList cursor = list.tail;
        while (cursor != null) {
            result = new OpExp(row, col, result, OpExp.COMMA, cursor.head);
            cursor = cursor.tail;
        }
        return result;
    }
:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal IF, ELSE, BOOL, INT, RETURN, VOID, WHILE;
terminal ASSIGN, EQ, NEQ, LT, LTEQ, GT, GTEQ, PLUS, MINUS, TIMES, OVER, AND, OR, UMINUS;
terminal LPAREN, RPAREN, LBRACE, RBRACE, LBRACK, RBRACK, SEMI, COMMA;
terminal String ID, NUM, TRUTH;
terminal ERROR;
   
non terminal ExpList program, declaration_list, local_declarations, statement_list, params, param_list, args, arg_list;
non terminal Exp declaration, var_declaration, fun_declaration, fun_prototype, type_specifier, param;
non terminal Exp statement, expression_stmt, selection_stmt, iteration_stmt, return_stmt, compound_stmt;
non terminal Exp expression, var;
non terminal Exp factor, call;
non terminal Integer relop;

precedence right ASSIGN;
precedence left OR;
precedence left AND;
precedence nonassoc EQ, NEQ, LT, LTEQ, GT, GTEQ;
precedence left PLUS, MINUS;
precedence left TIMES, OVER; 
precedence right UMINUS; 

/* ----------------------------Grammar Section-------------------- */
   
start with program;

program       ::= declaration_list:l
                  {: RESULT = l; :}
                ;

declaration_list
              ::= declaration_list:l declaration:d
                  {: RESULT = append(l, d); :}
                | declaration:d
                  {: RESULT = singleton(d); :}
                | declaration_list:l error SEMI
                  {: parser.report_error("invalid declaration", null);
                     RESULT = l;
                  :}
                ;

declaration   ::= var_declaration:d
                  {: RESULT = d; :}
                | fun_declaration:d
                  {: RESULT = d; :}
                | fun_prototype:p
                  {: RESULT = p; :}
                ;

type_specifier
              ::= INT:i
                  {: RESULT = new VarExp(ileft, iright, "int"); :}
                | VOID:v
                  {: RESULT = new VarExp(vleft, vright, "void"); :}
                | BOOL:b
                  {: RESULT = new VarExp(bleft, bright, "bool"); :}
                ;

var_declaration
              ::= type_specifier:t ID:i SEMI
                  {: RESULT = new AssignExp(ileft, iright, new VarExp(ileft, iright, i), t); :}
                | type_specifier:t ID:i LBRACK NUM:n RBRACK SEMI
                  {: RESULT = new AssignExp(ileft, iright,
                                            new VarExp(ileft, iright, i),
                                            new OpExp(ileft, iright, t, OpExp.LSB, new IntExp(nleft, nright, n)));
                  :}
                ;

fun_declaration
              ::= type_specifier:t ID:i LPAREN params:p RPAREN compound_stmt:c
                  {: Exp sig = new AssignExp(ileft, iright, new VarExp(ileft, iright, i), t);
                     Exp packedParams = packListAsCommaExpr(p, ileft, iright);
                     Exp header = new OpExp(ileft, iright, sig, OpExp.LHB, packedParams);
                     RESULT = new OpExp(ileft, iright, header, OpExp.RHB, c);
                  :}
                ;

fun_prototype ::= type_specifier:t ID:i LPAREN params:p RPAREN SEMI
                  {: Exp sig = new AssignExp(ileft, iright, new VarExp(ileft, iright, i), t);
                     Exp packedParams = packListAsCommaExpr(p, ileft, iright);
                     RESULT = new OpExp(ileft, iright, sig, OpExp.LHB, packedParams);
                  :}
                ;

params        ::= param_list:p
                  {: RESULT = p; :}
                | VOID:v
                  {: RESULT = singleton(new VarExp(vleft, vright, "void")); :}
                ;

param_list    ::= param_list:l COMMA param:p
                  {: RESULT = append(l, p); :}
                | param:p
                  {: RESULT = singleton(p); :}
                ;

param         ::= type_specifier:t ID:i
                  {: RESULT = new AssignExp(ileft, iright, new VarExp(ileft, iright, i), t); :}
                | type_specifier:t ID:i LBRACK RBRACK
                  {: RESULT = new AssignExp(ileft, iright,
                                            new VarExp(ileft, iright, i),
                                            new OpExp(ileft, iright, t, OpExp.LSB, null));
                  :}
                ;

compound_stmt ::= LBRACE local_declarations:l statement_list:s RBRACE
                  {: RESULT = new RepeatExp(lleft, lright, concat(l, s), null); :}
                ;

local_declarations
              ::= local_declarations:l var_declaration:d
                  {: RESULT = append(l, d); :}
                | /* empty */
                  {: RESULT = null; :}
                ;

statement_list
              ::= statement_list:l statement:s
                  {: RESULT = append(l, s); :}
                | /* empty */
                  {: RESULT = null; :}
                ;

statement     ::= expression_stmt:s
                  {: RESULT = s; :}
                | compound_stmt:s
                  {: RESULT = s; :}
                | selection_stmt:s
                  {: RESULT = s; :}
                | iteration_stmt:s
                  {: RESULT = s; :}
                | return_stmt:s
                  {: RESULT = s; :}
                ;

expression_stmt
              ::= expression:e SEMI
                  {: RESULT = e; :}
                | SEMI:s
                  {: RESULT = new WriteExp(sleft, sright, null); :}
                ;

selection_stmt
              ::= IF:i LPAREN expression:e RPAREN statement:s
                  {: RESULT = new IfExp(ileft, iright, e, singleton(s), null); :}
                | IF:i LPAREN expression:e RPAREN statement:s1 ELSE statement:s2
                  {: RESULT = new IfExp(ileft, iright, e, singleton(s1), singleton(s2)); :}
                ;

iteration_stmt
              ::= WHILE:w LPAREN expression:e RPAREN statement:s
                  {: RESULT = new RepeatExp(wleft, wright, singleton(s), e); :}
                ;

return_stmt   ::= RETURN:r SEMI
                  {: RESULT = new WriteExp(rleft, rright, null); :}
                | RETURN:r expression:e SEMI
                  {: RESULT = new WriteExp(rleft, rright, e); :}
                ;

expression    ::= var:v ASSIGN expression:e
                  {: RESULT = new OpExp(vleft, vright, v, OpExp.ASSIGN, e); :}
                | expression:l OR expression:r
                  {: RESULT = new OpExp(lleft, lright, l, OpExp.OR, r); :}
                | expression:l AND expression:r
                  {: RESULT = new OpExp(lleft, lright, l, OpExp.AND, r); :}
                | expression:l relop:o expression:r
                  {: RESULT = new OpExp(lleft, lright, l, o.intValue(), r); :}
                | expression:l PLUS expression:r
                  {: RESULT = new OpExp(lleft, lright, l, OpExp.PLUS, r); :}
                | expression:l MINUS expression:r
                  {: RESULT = new OpExp(lleft, lright, l, OpExp.MINUS, r); :}
                | expression:l TIMES expression:r
                  {: RESULT = new OpExp(lleft, lright, l, OpExp.TIMES, r); :}
                | expression:l OVER expression:r
                  {: RESULT = new OpExp(lleft, lright, l, OpExp.OVER, r); :}
                | MINUS expression:e %prec UMINUS
                  {: RESULT = new OpExp(mleft, mright, null, OpExp.UMINUS, e); :}
                | UMINUS expression:e %prec UMINUS
                  {: RESULT = new OpExp(nleft, nright, null, OpExp.BOOL, e); :}
                | factor:f
                  {: RESULT = f; :}
                ;

relop         ::= LTEQ
                  {: RESULT = Integer.valueOf(OpExp.LTEQ); :}
                | LT
                  {: RESULT = Integer.valueOf(OpExp.LT); :}
                | GT
                  {: RESULT = Integer.valueOf(OpExp.GT); :}
                | GTEQ
                  {: RESULT = Integer.valueOf(OpExp.GTEQ); :}
                | EQ
                  {: RESULT = Integer.valueOf(OpExp.EQ); :}
                | NEQ
                  {: RESULT = Integer.valueOf(OpExp.NEQ); :}
                ;

factor        ::= LPAREN expression:e RPAREN
                  {: RESULT = e; :}
                | LPAREN error RPAREN
                  {: parser.report_error("invalid expression", null);
                     RESULT = null;
                  :}
                | var:v
                  {: RESULT = v; :}
                | call:c
                  {: RESULT = c; :}
                | NUM:n
                  {: RESULT = new IntExp(nleft, nright, n); :}
                | TRUTH:t
                  {: RESULT = new VarExp(tleft, tright, t); :}
                ;

call          ::= ID:i LPAREN args:a RPAREN
                  {: Exp packedArgs = packListAsCommaExpr(a, ileft, iright);
                     RESULT = new OpExp(ileft, iright, new VarExp(ileft, iright, i), OpExp.LHB, packedArgs);
                  :}
                ;

args          ::= arg_list:l
                  {: RESULT = l; :}
                | /* empty */
                  {: RESULT = null; :}
                ;

arg_list      ::= arg_list:l COMMA expression:e
                  {: RESULT = append(l, e); :}
                | expression:e
                  {: RESULT = singleton(e); :}
                ;

var           ::= ID:i
                  {: RESULT = new VarExp(ileft, iright, i); :}
                | ID:i LBRACK expression:e RBRACK
                  {: RESULT = new OpExp(ileft, iright, new VarExp(ileft, iright, i), OpExp.LSB, e); :}
                ;